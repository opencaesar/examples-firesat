@dc:title "Flight System Engineering"
vocabulary <http://opencaesar.io/firesat/vocabularies/fse#> as fse {

	extends <http://purl.org/dc/elements/1.1/> as dc

	extends <http://www.w3.org/2001/XMLSchema#> as xsd

	extends <http://imce.jpl.nasa.gov/foundation/base#> as base

	extends <http://imce.jpl.nasa.gov/foundation/mission#> as mission

	extends <http://imce.jpl.nasa.gov/foundation/project#> as project

	extends <http://bipm.org/jcgm/vim4#> as vim4

	uses <http://iso.org/iso-80000-4.1#> as iso-80000-4.1

	// Quantities

	aspect MassConstrainedElement < vim4:Object

	concept MassConstraint < vim4:InherentUnitaryQuantityValue [
		restricts all vim4:characterizes to MassConstrainedElement
    	restricts vim4:instantiates to iso-80000-4.1:mass
    	restricts vim4:unit to iso-80000-4.1:kilogram
	]

	concept MassAllocationConstraint < MassConstraint

	concept MassLimitConstraint < MassConstraint

	// Workpackages

	ref concept project:WorkPackage < MassConstrainedElement

	// Components

	concept Subsystem < mission:Component, MassConstrainedElement [
		restricts all base:aggregates to Assembly
	]
	
	concept Assembly < mission:Component, MassConstrainedElement [
		restricts all base:isAggregatedIn to Subsystem
	]

	// Interfaces

	concept EndCircuit < mission:Interface [
		restricts all mission:isJoinedIn to PowerFunction
		restricts all mission:isPresentedBy to Assembly
	]

	concept PowerEndCircuit < EndCircuit

	concept PowerIn < PowerEndCircuit

	concept PowerOut < PowerEndCircuit

	// Junctions

	concept Function < mission:Junction [
		restricts all mission:joins to EndCircuit
	]

	scalar property hasFunctionType [
		domain Function
		range xsd:string
	]
	
	concept PowerFunction < Function [
		restricts hasFunctionType to "Power"
		restricts all mission:joins to PowerEndCircuit
		
	]

	relation entity ConnectsAssembly [
		from Function
		to Assembly
		forward connectsAssembly
		asymmetric
		irreflexive
	]

	relation entity ConnectsAssembly1 [
		from Function
		to Assembly
		forward connectsAssembly1
		asymmetric
		irreflexive
	] < ConnectsAssembly

	relation entity ConnectsAssembly2 [
		from Function
		to Assembly
		forward connectsAssembly2
		asymmetric
		irreflexive
	] < ConnectsAssembly 

	scalar EndCircuitDirection [
		oneOf "in"^^xsd:string,
			"inout"^^xsd:string,
			"out"^^xsd:string
	]

	scalar property hasFunctionDirection [
		domain Function
		range FunctionDirection
	]

	scalar FunctionDirection [
		oneOf "1to2"^^xsd:string,
			"2to1"^^xsd:string,
			"Bidirectional"^^xsd:string,
			"Undefined"^^xsd:string
	]
	
	scalar property hasEndCircuitDirectione [
		domain Function
		range EndCircuitDirection
	]

}